apply plugin: 'com.android.application'
//apply plugin: 'io.fabric'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "com.duy.compiler.javanide"
        minSdkVersion rootProject.ext.minSdkVersion
        //noinspection OldTargetApi
        targetSdkVersion 28
        multiDexEnabled true
        versionCode 45
        versionName "1.4.5"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            res.srcDirs += "src/main/res-color-picker"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Map for the version code that gives each ABI a value.
    applicationVariants.all { variant ->
        Properties props = new Properties()
        def propFile = new File(rootProject.projectDir, "signing.properties")
        if (propFile.exists()) {
            props.load(new FileInputStream(propFile))
            buildConfigField "String", "BASE64_KEY", "\"" + props['BASE64_KEY'] + "\""
            buildConfigField "String", "SKU_PREMIUM", "\"" + props['SKU_PREMIUM'] + "\""
        } else {
            buildConfigField "String", "BASE64_KEY", "\"\""
            buildConfigField "String", "SKU_PREMIUM", "\"\""
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/plexus/components.xml'
        exclude 'META-INF/INDEX.LIST'
    }
    compileOptions {
        targetCompatibility = 1.8
        sourceCompatibility = 1.8
    }
}


configurations {
    all {
        exclude group: 'commons-logging', module: 'commons-logging'
        resolutionStrategy.force 'junit:junit:4.12'
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])

    api project(':treeview')
    api project(':lib-android-compiler')
    api project(':lib-google-java-format')
    api project(':androidlogcat')
    api project(':common')

    api project(':lib-n-ide')
    //api project(':layout-inflater')

    //firebase sdk
    //api "com.google.firebase:firebase-core:16.0.0"
    //api 'com.crashlytics.sdk.android:crashlytics:2.9.3'

    api 'io.github.kobakei:ratethisapp:1.2.0'
    api 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    api 'com.miguelcatalan:materialsearchview:1.4.0'

    /*Lib n-ide*/
    api 'com.google.code.gson:gson:2.8.5'
    api 'com.anjlab.android.iab.v3:library:1.1.0'
    api 'org.apmem.tools:layouts:1.10@aar'
    api 'com.makeramen:roundedimageview:2.2.1'
    api 'com.yqritc:recyclerview-flexibledivider:1.2.9'
    api 'com.sothree.slidinguppanel:library:3.4.0'
    //noinspection GradleDependency newer version cause crash
    api ('commons-io:commons-io:2.3'){
        transitive = true
    }

    api 'com.simplecityapps:recyclerview-fastscroll:1.0.18'
    api 'org.apache.commons:commons-lang3:3.7'
    api 'com.google.guava:guava:25.1-android'
    api 'com.ogaclejapan.smarttablayout:library:1.6.1@aar'

    //android compat
    api 'androidx.multidex:multidex:2.0.1'
    api 'androidx.recyclerview:recyclerview:1.0.0'
    api 'androidx.appcompat:appcompat:1.1.0'
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'androidx.vectordrawable:vectordrawable:1.1.0'
    api 'androidx.cardview:cardview:1.0.0'
    api 'androidx.annotation:annotation:1.1.0'
    api 'com.google.android.material:material:1.0.0'
    api 'androidx.preference:preference:1.1.0'
}

apply plugin: 'com.google.gms.google-services'